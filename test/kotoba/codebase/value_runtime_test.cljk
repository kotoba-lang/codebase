(ns kotoba.codebase.value-runtime-test
  (:require [clojure.test :refer [deftest is]]
            [ipld.value :as value]
            [kotoba.codebase.store :as store]
            [kotoba.codebase.value-runtime :as runtime]
            [multiformats.core :as mf]))

(defn- temp-store []
  (.toFile (java.nio.file.Files/createTempDirectory
            "kotoba-value-runtime-"
            (make-array java.nio.file.attribute.FileAttribute 0))))

(defn- problem-of [f]
  (try (f) nil
       (catch clojure.lang.ExceptionInfo e (:problem (ex-data e)))))

(defn- with-store [f]
  (let [root (temp-store)]
    (try
      (store/initialize! root)
      (f root)
      (finally
        (doseq [file (reverse (file-seq root))] (.delete ^java.io.File file))))))

(deftest cid-is-global-and-handle-is-run-local
  (with-store (fn [root]
    (let [a (runtime/create root)
          b (runtime/create root)
          value {:name "Jun" :age 30}
          ha (runtime/intern! a value)
          cid (runtime/cid-of a ha)
          hb (runtime/hydrate! b cid)]
      (is (= cid (mf/cidv1-dag-cbor (value/encode-value value))))
      (is (= value (runtime/resolve a ha)))
      (is (= value (runtime/resolve b hb)))
      (is (= ha (runtime/intern! a (array-map :age 30 :name "Jun")))
          "equal content is interned once within a run")))))

(deftest returned-bytes-cannot-mutate-the-interned-value
  (with-store (fn [root]
    (let [rt (runtime/create root)
          handle (runtime/intern! rt (byte-array [1 2 3]))
          first-read ^bytes (runtime/resolve rt handle)]
      (aset-byte first-read 0 (byte 99))
      (is (= [1 2 3] (vec (runtime/resolve rt handle))))))))

(deftest handles-are-bounded-checked-and-never-reused
  (with-store (fn [root]
    (let [rt (runtime/create root 2)
          h1 (runtime/intern! rt :one)
          h2 (runtime/intern! rt :two)]
      (is (= :value-runtime/exhausted
             (problem-of #(runtime/intern! rt :three))))
      (is (= :value-runtime/invalid-handle
             (problem-of #(runtime/resolve rt 0))))
      (is (= :value-runtime/unknown-handle
             (problem-of #(runtime/resolve rt 999))))
      (runtime/release! rt h1)
      (is (= :value-runtime/unknown-handle
             (problem-of #(runtime/resolve rt h1))))
      (let [h3 (runtime/intern! rt :three)]
        (is (> h3 h2) "release cannot create an ABA handle reuse"))))))

(deftest runtime-lifetime-and-cas-lifetime-are-separate
  (with-store (fn [root]
    (let [rt (runtime/create root)
          handle (runtime/intern! rt {:persistent true})
          cid (runtime/cid-of rt handle)]
      (runtime/close! rt)
      (is (= :value-runtime/closed
             (problem-of #(runtime/resolve rt handle))))
      (let [next-run (runtime/create root)
            next-handle (runtime/hydrate! next-run cid)]
        (is (= {:persistent true} (runtime/resolve next-run next-handle))))))))

(deftest authority-bearing-host-objects-are-not-values-by-accident
  (with-store (fn [root]
    (let [rt (runtime/create root)]
      (is (= :value/record-unsupported
             (problem-of #(runtime/intern! rt rt)))
          "a runtime/capability-like record cannot cross the value codec")))))
