(ns kotoba.codebase.value-runtime-abi-test
  (:require [clojure.test :refer [deftest is]]
            [ipld.value :as value]
            [kotoba.codebase.store :as store]
            [kotoba.codebase.value-runtime :as runtime]
            [kotoba.codebase.value-runtime-abi :as abi]))

(defn- with-runtime [f]
  (let [root (.toFile (java.nio.file.Files/createTempDirectory
                       "kotoba-value-abi-"
                       (make-array java.nio.file.attribute.FileAttribute 0)))]
    (try
      (store/initialize! root)
      (f root (runtime/create root))
      (finally
        (doseq [file (reverse (file-seq root))] (.delete ^java.io.File file))))))

(defn- problem-of [f]
  (try (f) nil
       (catch clojure.lang.ExceptionInfo e (:problem (ex-data e)))))

(deftest one-contract-roundtrips-across-backend-transports
  (with-runtime
    (fn [root rt]
      (let [encoded (value/encode-value {:name "Jun" :age 30})
            handle (:handle (abi/dispatch! rt {:op :value/intern :payload encoded}))
            cid (:cid (abi/dispatch! rt {:op :value/cid-of :payload handle}))
            resolved (:bytes (abi/dispatch! rt {:op :value/resolve :payload handle}))
            other (runtime/create root)
            hydrated (:handle (abi/dispatch! other {:op :value/hydrate :payload cid}))]
        (is (= abi/abi-id (:abi (abi/dispatch! rt {:op :value/cid-of
                                                    :payload handle}))))
        (is (= {:name "Jun" :age 30} (value/decode-value resolved)))
        (is (= {:name "Jun" :age 30} (runtime/resolve other hydrated)))
        (is (= cid (runtime/cid-of other hydrated)))))))

(deftest abi-fails-closed-before-a-backend-can-confuse-identities
  (with-runtime
    (fn [_ rt]
      (is (= :value-abi/invalid-request
             (problem-of #(abi/dispatch! rt {:op :value/intern
                                              :payload (byte-array 0)
                                              :capability :forged}))))
      (is (= :value-abi/unknown-operation
             (problem-of #(abi/dispatch! rt {:op :capability/read
                                              :payload 1}))))
      (is (= :value-abi/not-bytes
             (problem-of #(abi/dispatch! rt {:op :value/intern
                                              :payload "not bytes"}))))
      (is (= :value-abi/invalid-cid-text
             (problem-of #(abi/dispatch! rt {:op :value/hydrate
                                              :payload ""}))))
      (is (= :value-abi/invalid-cid-text
             (problem-of #(abi/dispatch! rt {:op :value/hydrate
                                              :payload "../STORE.edn"})))))))

(deftest release-is-an-explicit-abi-operation
  (with-runtime
    (fn [_ rt]
      (let [handle (:handle (abi/dispatch! rt
                                           {:op :value/intern
                                            :payload (value/encode-value :x)}))
            released (abi/dispatch! rt {:op :value/release :payload handle})]
        (is (:released? released))
        (is (= :value-runtime/unknown-handle
               (problem-of #(abi/dispatch! rt {:op :value/resolve
                                                :payload handle}))))))))

(deftest kir-and-wasm-use-the-envelope-free-adapter
  (with-runtime
    (fn [_ rt]
      (let [call (abi/as-value-call rt)
            handle (call :value/intern (value/encode-value {:portable true}))
            cid (call :value/cid-of handle)]
        (is (pos-int? handle))
        (is (string? cid))
        (is (= {:portable true} (value/decode-value (call :value/resolve handle))))
        (is (= 1 (call :value/release handle)))
        (is (= :value-runtime/unknown-handle
               (problem-of #(call :value/resolve handle))))))))
